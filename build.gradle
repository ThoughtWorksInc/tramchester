buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:2.1.0'
    }
}

plugins {
    id "com.github.ManifestClasspath" version "0.1.0-RELEASE"
}

apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

mainClassName = 'com.tramchester.App'

def candidate = System.getenv("CIRCLE_BUILD_NUM") ?: "0"
// override build number if RELEASE_NUMBER set, needed because of way circle ci workflows operate
def buildNumber = System.getenv("RELEASE_NUMBER") ?: candidate
def environment = System.getenv("ENV") ?: "Dev"
def isWindows = System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')

// this is used in the generated dist zip filename
version = 1.0

project.ext {
    dropwizardVersion = '1.3.18'
}

repositories {
    maven {
        url "https://repo.osgeo.org/repository/release/"
    }
    mavenCentral()
    maven {
        url "https://raw.github.com/neo4j-contrib/m2/master/releases"
    }
}

dependencies {
    compile('io.dropwizard:dropwizard-core:' + dropwizardVersion) { exclude group: 'asm', module: 'asm' }
    compile('io.dropwizard:dropwizard-assets:' + dropwizardVersion) { exclude group: 'asm', module: 'asm' }
    compile 'org.picocontainer:picocontainer:2.15',
            'org.neo4j:neo4j:3.5.14',
            'org.neo4j:neo4j-slf4j:3.5.14',
            'org.geotools:gt-main:22.2',
            'org.geotools:gt-epsg-hsql:22.2',
            'com.github.cliftonlabs:json-simple:3.1.1',
            'commons-io:commons-io:2.6',
            'org.apache.commons:commons-csv:1.8',
            'org.apache.httpcomponents:httpclient:4.5.11',
            'org.apache.commons:commons-collections4:4.4',
            'com.amazonaws:aws-java-sdk-cloudwatch:1.11.723',
            'com.amazonaws:aws-java-sdk-s3:1.11.723',
            'com.smoketurner:dropwizard-swagger:1.3.17-1',
            'io.dropwizard-bundles:dropwizard-configurable-assets-bundle:1.3.5',
            'com.github.ben-manes.caffeine:caffeine:2.8.1',
            'software.amazon.awscdk:core:1.31.0',
            'software.amazon.awscdk:s3:1.31.0',
            'software.amazon.awscdk:cdk-cx-api:1.31.0'

    testCompile 'junit:junit:4.13',
            'org.assertj:assertj-core:1.2.0',
            'io.dropwizard:dropwizard-testing:' + dropwizardVersion,
            'org.easymock:easymock:4.2',
            'org.seleniumhq.selenium:selenium-java:3.141.59',
            'io.appium:java-client:7.2.0',
            'org.hamcrest:hamcrest:2.1'

    dependencies {
        // JAX-B dependencies for JDK 9+
        implementation "jakarta.xml.bind:jakarta.xml.bind-api:2.3.2"
        implementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"
        implementation "com.google.guava:guava:28.2-jre"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['main/src']
        }
    }
    test {
        java {
            srcDirs = ['main/test']
        }
        resources.srcDir file('main/test/resources')
    }
}

processResources {
    exclude('**/app/javascript') // dist via webpack
}

task cfnassist {
    doLast {
        ant.taskdef(name: 'cfnassist', classname: 'tw.com.ant.CfnAssistAntTask') {
            classpath {
                fileset(dir: 'lib/cfnassist-1.1.37/lib/', includes: 'cfnassist-all-1.1.37.jar')
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.deprecation = true
    options.compilerArgs.addAll(['-Xlint'])
}

task stage(dependsOn: ['clean', 'installApp'])

task cleanGraph() {
    doLast {
        delete 'databases/integrationBusTest' , 'databases/integrationTramTest', 'buses.db',
                'tramchester.db', 'databases/tramchesterAcceptance.db'
    }
}

clean {
    delete 'src/main/resources/app/dist/'
    delete 'data/tram/data.zip', 'data/bus/data.zip'
    delete fileTree('data/tram') {
        include '*.txt'
    }
    delete fileTree('data/tram/gtdf-out') {
        include '*.txt'
    }
    delete 'data/tram/gtdf-out'
    delete fileTree('data/bus') {
        include '*.txt'
    }
    delete fileTree('data/bus/gtdf-out') {
        include '*.txt'
    }
    delete 'data/bus/gtdf-out'
    delete fileTree('data/codepo_gb') {
        include '**'
    }
    //cleanGraph
}

run {
    dependsOn 'webpack'
    args 'server', 'config/local.yml'
}

task pullData(type:JavaExec, dependsOn:classes) {
    main = 'com.tramchester.dataimport.FetchDataFromUrl'
    classpath = sourceSets.main.runtimeClasspath
    args 'http://odata.tfgm.com/opendata/downloads/TfGMgtfs.zip', 'data', 'tramData-1.0.zip'
}

task allowhost(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task blockhost(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            BlackList(port: '443', tag: 'web')
        }
    }
}

task buses(type:JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args 'server', 'config/buses.yml'
}

task busTest(type: Test) {
    useJUnit {
        includeCategories 'com.tramchester.testSupport.BusTest'
    }
}

task unit(type: Test, dependsOn: compileJava) {
    useJUnit()
    filter {
        includeTestsMatching "com.tramchester.unit.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started",  "standardError"
    }
}

task integration(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "1500m"
    useJUnit {
        excludeCategories "com.tramchester.testSupport.LiveDataMessagesCategory" // hopefully temporary exclusion
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }

    forkEvery = 10 // bascially a workaround for memory growth over time, which TODO needs to get fixed!
}

task allStationsTest(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "2500m"
    useJUnit()
    filter {
        includeTestsMatching "com.tramchester.integration.graph.RouteCalculatorTestAllJourneys"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started" , "standardError", "standardOut"
    }
}

task buildGraphTest(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "2500m"
    useJUnit()
    filter {
        includeTestsMatching "com.tramchester.integration.graph.GraphBuildAndStartTest"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError", "standardOut"
    }
}

task appium(type: Test) {
    dependsOn 'allowhost'
    dependsOn 'compileJava'
    dependsOn 'webpack'
    useJUnit()
    filter {
        includeTestsMatching "com.tramchester.acceptance.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" , "standardOut"
    }
    finalizedBy {
        blockhost
    }
}

task localAppium(type: Test) {
    dependsOn 'webpack'
    dependsOn 'compileJava'
    minHeapSize = "1000m"
    maxHeapSize = "2500m"
    useJUnit()
    filter {
        includeTestsMatching "com.tramchester.acceptance.*"
    }
    testLogging {
        events "passed", "skipped", "failed", "started", "standardError" , "standardOut"
    }
}

task uploadData(dependsOn: ['cfnassist', 'pullData']) {
    doLast {
        ant.cfnassist(buildNumber: buildNumber, bucketname: 'tramchester2dist') {
            Artifact(name: 'data', value: 'data/tramData-1.0.zip')
            S3Create()
        }
    }
}

task diagrams(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist() {
            Diagrams(target: '.')
        }
    }
}

task npmInstall(type: Exec) {
    inputs.file("package.json")
    outputs.dir("node_modules")

    if (isWindows) {
        commandLine "npm.cmd", "install"
    }
    else {
        commandLine "npm", "install"
    }
}

jar {
    dependsOn 'webpack'
    into 'app', {
        from 'src/main/resources/app'
    }
}

task webpack(type: Exec, dependsOn: npmInstall) {
    inputs.file("src/main/resources/app/index.html")
    inputs.dir("src/main/resources/app/javascript")
    inputs.dir("src/main/resources/app/css")
    inputs.file("webpack.config.js")

    outputs.file("src/main/resources/app/dist/main.js")
    outputs.file("src/main/resources/app/dist/map.js")

    if (isWindows) {
        commandLine "./node_modules/.bin/webpack.cmd"
    } else {
        commandLine "./node_modules/.bin/webpack"
    }
}

distZip {
    into('config') {
        from 'config'
    }
}

task uploadApp(dependsOn: ['cfnassist', 'distZip', 'uploadSupportFiles'])  {
    doLast {
        ant.cfnassist(buildNumber: buildNumber, bucketname: 'tramchester2dist') {
            Artifact(name: 'package', value: 'build/distributions/tramchester-1.0.zip')
            S3Create()
        }
    }
}

task uploadSupportFiles(dependsOn: 'cfnassist') {
    doLast {
        ant.cfnassist(buildNumber: buildNumber, bucketname: 'tramchester2dist') {
            Artifact(name: 'install', value: 'deploy/setupTramWebServer.sh')
            Artifact(name: 'cloudinit', value: 'deploy/cloudInit.txt')
            Artifact(name: 'install', value: 'deploy/setupTramWebServerAWSLinux.sh')
            Artifact(name: 'cloudinit', value: 'deploy/cloudInitAWSLinux.txt')
            S3Create()
        }
    }
}

task infra(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, sns: 'true', capabilityIAM: 'true') {
            Templates(target: 'deploy/infra/'+environment )
        }
    }
}

task upload(dependsOn: ['uploadData', 'uploadApp'])

task deploy(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            Templates(target: 'deploy/servers.json')
        }
    }
}

task updateLB(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            ELBUpdate(typeTag: 'web')
        }
    }
}

task tidy(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            TidyStacks(target: 'deploy/servers.json', typeTag: 'web')
        }
    }
}

task allowHostDev(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'Dev') {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task blockHostDev(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'Dev') {
            BlackList(port: '443', tag: 'web')
        }
    }
}

task whitelistUAT(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'UAT') {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task blacklistUAT(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'UAT') {
            BlackList(port: '443', tag: 'web')
        }
    }
}






